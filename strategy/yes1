INPUT : Buy_trail(10),p1(5),p2(20),p3(60);
var : top(0),bot(0),fast(0),mid(0),slow(0),mult(0),recnt(0),width_sum(0),i(0),bet(0);

fast = ma(c,p1);
mid = ma(c,p2);
slow = ma(c,p3);

width_sum = 0;
for i = 1 to 3 begin
	width_sum = width_sum + (h[i] - l[i])/medianprice[i];	# 밴드폭
end;

##### 레버리지 조절 #####
if positionprofit(1) < 0 then bet = 5;	# 최근 거래에서 손실 발생시 레버리지 5배
else if width_sum * 100 <= 0.5 then bet = 30;	# 변동성이 0.5 이하면 레버레지 30배 
else if width_sum * 100 <= 1 then bet = 20;		# 변동성이 1 이하면 레버리지 20배
else bet = 10;	# 그 외에 레버리지는 최소 10배
#####

condition1 = fast > mid and mid > slow ;	# 골든크로스
condition2 = countif(crossup(fast,mid),15) >= 1 or countif(crossup(mid,slow),15) >= 1;
# 최근 15개 봉에서 단기이평이 중기 이평을 상향돌파한 횟수가 1회 이상이거나 중기이평이 장기이폄을 상향돌파한 횟수가 1 이상일 경우 

if condition2 == True then mult = 1.015; else mult = 1.0001;
# 상기 조건을 만족하면 1% 가중치 추가 ==> 휩소에 휘말리지 않기 위함

top = highest(h,buy_trail) * mult;

if marketposition == 0 and condition1 then begin
	if totaltrades == 0 or totaltrades >= 1 and barssinceexit(1) >= 5 then buy("Entry",atstop,top,bet);
end;

#### 손절 #####
if currentcontracts == 30 then setstoploss(1);  # 레버리지 30배 : 손절 1%
else if currentcontracts == 20 then setstoploss(1.5);	# 레버리지 20배 : 손절 1.5%
else setstoploss(3);	# 레버리지 10배 이하 : 손절은 2.5%		
#####

if marketposition == 1 then begin
	if currentcontracts == 30 then exitlong("EL1",ATLIMIT,ENTRYPRICE * 1.01);   # 레버리지 30배 : 진입가 * 1.01 지정가로 청산
	else if currentcontracts == 20 then exitlong("EL2",ATLIMIT,ENTRYPRICE * 1.015); # 레버리지 20배 : 진입가 * 1.05 지정가로 청산
	else exitlong("EL3",ATLIMIT,ENTRYPRICE * 1.02); # 레버리지 20배 미만 진입가 * 1.02 지정가로 청산

END;
